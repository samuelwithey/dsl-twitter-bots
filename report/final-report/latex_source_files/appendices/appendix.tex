\setlength{\grammarparsep}{2pt plus 1pt minus 1pt} % increase separation between rules
\setlength{\grammarindent}{15em} % increase separation between LHS/RHS

\begin{appendices}

\chapter{Final Grammar Design in EBNF}

\begin{grammar}
<twitbot> ::= (<statement> `;')+ 

<statement> ::= <action> 

<action> ::= <tweet>
\alt <tweetImage>
\alt <reply>
\alt <retweet>
\alt <favourite>
\alt <scheduleTweet>
\alt <directMessage>
\alt <autoFavouriteRetweet>
\alt <autoFollowFollowers>
\alt <autoReplyMentions>

<tweet> ::= `tweet' <tweet_req_param> \newline (`,' <tweet_optional_params>)*

<tweet_req_param> ::= `status' `:' <string>

<tweet_optional_params> ::= `possibly_sensitive' `:' <boolean>
\alt `lat' `:' <number>
\alt `long' `:' <number>
\alt `display_coordinates' `:' <boolean>

<tweetImage> ::= `tweet_image' <tweet_req_param> \newline `,' <tweetImage_req_param> \newline (`,' <tweet_optional_params>)*

<tweetImage_req_param> ::= `image_name' `:' <string>

<reply> ::= `reply_to_tweet' <reply_req_params> \newline (`,' <tweetImage_req_param>)? \newline (`,' <tweet_optional_params>)*

<reply_req_params> ::= `in_reply_to_status_id' `:' <number> `,' \newline `status' `:' <string>

<retweet> ::= `retweet' `id' `:' <number>

<favourite> ::= `favourite' `id' `:' <number>

<scheduleTweet> ::= `schedule' <scheduleTweet_req_param>

<scheduleTweet_req_param> ::= <date_time_param> `,' (<tweet> \alt <tweetImage>)

<date_time_param> ::= <minute> `,' <hour> `,' <day_of_month> `,' <month>

<minute> ::= `minute' `:' <numeric_minute>

<hour> ::= `hour' `:' <numeric_hour>

<day_of_month> ::= `day_of_month' `:' <numeric_day>

<month> ::= `month' `:' <numeric_month>

<directMessage> ::= `direct_message' <directMessage_req_params>

<directMessage_req_params> ::= `recipient_id' `:' <number> `,' `text' `:' <string>

<autoFavouriteRetweet> ::= `auto_fav_retweet' <keyword> (`,' <keyword>)* 

<autoFollowFollowers> ::= `follow_all_followers'

<autoReplyMentions> ::= `automate_reply_to_mentions' \newline <automateReply_req_param> (`,' <keyword>)+

<automateReply_req_param> ::= `automate_time_minutes' `:' <numeric_minute> `,' \newline `response' `:' <string>

<string> ::= [a-zA-Z0-9]+

<keyword> ::= `keyword' `:' <string>

<number> ::= <unary_operator>? <unsigned_number>

<unary_operator> ::= `+' \alt `-'

<unsigned_number> ::= <unsigned_int>
\alt <unsigned_float>

<unsigned_int> ::= (<digit>)+

<unsigned_float> ::= (<digit>)+ '.' (<digit>)*

<digit> ::= [0-9]

<boolean> ::= `True'
\alt `False'

<numeric_month> ::= 0[1-9]\alt1[0-2]

<numeric_day> ::= 0[1-9]\alt1[0-9]\alt2[0-9]\alt3[0-1]

<numeric_hour> ::= 0[0-9]\alt1[0-9]\alt2[0-3]

<numeric_minute> ::= 0[0-9]\alt1[0-9]\alt2[0-9]\alt3[0-9]\alt4[0-9]\alt5[0-9]

\end{grammar}

\end{appendices}